resource "volterra_app_firewall" "recommended" {
  name      = "${var.uk_se_name}-waf-${var.short_name}"
  namespace = var.volterra_namespace

  allow_all_response_codes = true
  default_anonymization = true
  use_default_blocking_page = true
  default_bot_setting = true
  default_detection_settings = true
  use_loadbalancer_setting = true
}

resource "volterra_healthcheck" "health" {
  name      = "${var.uk_se_name}-${var.short_name}"
  namespace = var.volterra_namespace

  tcp_health_check {
  }
  healthy_threshold   = 3
  interval            = 15
  timeout             = 3
  unhealthy_threshold = 1
}

resource "volterra_origin_pool" "origin" {
  name                   = "${var.uk_se_name}-${var.short_name}"
  namespace              = var.volterra_namespace
  description            = "Terraform created origin pool"
  loadbalancer_algorithm = "LB_OVERRIDE"

  origin_servers {
    public_name {
      dns_name = var.origin_fqdn
    }
  }
  port               = 443
  endpoint_selection = "LOCAL_PREFERRED"
  use_tls {
    no_mtls                  = true
    skip_server_verification = true
    tls_config {
      default_security = true
    }
    use_host_header_as_sni = true
  }
  healthcheck {
    name      = volterra_healthcheck.health.name
    namespace = var.volterra_namespace
  }
}

resource "volterra_http_loadbalancer" "lb" {
  name      = "${var.uk_se_name}-${var.short_name}"
  namespace = var.volterra_namespace
  domains   = [var.lb_fqdn]

  advertise_on_public_default_vip = true
  no_challenge                    = true
  round_robin                     = true
  disable_rate_limit              = true
  no_service_policies             = true
  multi_lb_app                    = true
  user_id_client_ip               = true
  #disable_waf                     = true

  app_firewall {
    name      = volterra_app_firewall.recommended.name
    namespace = var.volterra_namespace
  }

  https_auto_cert {
    add_hsts               = true
    http_redirect          = true
    no_mtls                = true
    default_header         = true
    disable_path_normalize = true


    tls_config {
      default_security = true
    }
  }

  default_route_pools {
    pool {
      name      = volterra_origin_pool.origin.name
      namespace = var.volterra_namespace
    }
    weight = 1
  }
  routes {
    simple_route {
      http_method = "ANY"
      path {
        regex = "(.*?)"
      }
      origin_pools {
        pool {
          name      = volterra_origin_pool.origin.name
          namespace = var.volterra_namespace
        }
        weight = 1
      }
      headers {
        name = "Host"
        exact = "adminae53.volterra.link"
      }
      host_rewrite = var.vanity_fqdn
    }
  }
}
